## Section 108

> Begin metadata

```catala
declaration scope TaxPayer:
  # Inputs
  # ------

  # See insolvency worksheet https://www.irs.gov/pub/irs-pdf/p4681.pdf page 6
  context fmv_assets content money
  context liabilities content money

  # Income from discharge of indebtedness, as introduced in 61(a)
  context discharge_of_indebtedness content money

  # Outputs
  # -------

  # By how much the taxpayer is insolvent, $0 otherwise
  context insolvency content money
  # Whether the taxpayer is insolvent or not -- a boolean explicitly defined in
  # https://www.irs.gov/pub/irs-pdf/p4681.pdf page 6 line 38, but implicitly
  # defined in the Tax Code, which "puns" on insolvent vs. insolvency in
  # 108(d)(3).
  context is_insolvent content boolean

declaration scope TaxReturn:
  # Input
  context taxpayer scope TaxPayer

  # Input-Output: initially $0, then various scopes (e.g. SalaryIncome,
  # CapitalGains, and our very own DischargeOfIndebtedness) increase this
  # quantity with their own respective amounts.
  context gross_income content money

declaration scope DischargeOfIndebtedness:
  # Input-Output: we override return.gross_income
  context return scope TaxReturn

  # Internal: this scope computes whatever amount from (11) should go towards
  # the gross income
  context gross_income11 content money
```

> End metadata

26 U.S. Code § 61 - Gross income defined

(a) General definition. Except as otherwise provided in this subtitle, gross
income means all income from whatever source derived, including (but not limited
to) the following items:

  (1) Compensation for services, including fees, commissions, fringe benefits, and similar items;
  (2) Gross income derived from business;
  (3) Gains derived from dealings in property;
  (4) Interest;
  (5) Rents;
  (6) Royalties;
  (7) Dividends;
  (8) Annuities;
  (9) Income from life insurance and endowment contracts;
  (10) Pensions;
  (11) Income from discharge of indebtedness;
  (12) Distributive share of partnership gross income;
  (13) Income in respect of a decedent; and
  (14) Income from an interest in an estate or trust.

```catala
scope DischargeOfIndebtedness:
  replace definition return.gross_income equals
    # whatever has been previously counted towards gross income (e.g.
    # CapitalGains) by other scopes
    return.gross_income +
    # plus what counts for "line 11", i.e. the quantity computed by the scope
    # DischargeOfIndebtedness
    gross_income11

  # This says: by default, the income for line (11) *is* the taxpayer's
  # discharge of indebtedness. As we will see, further rules will refine what
  # goes into line (11).
  label delta_3
  definition gross_income11 equals
    taxpayer.discharge_of_indebtedness
```

108(d)(3) Insolvent

For purposes of this section, the term “insolvent” means the excess of
liabilities over the fair market value of assets. With respect to any discharge,
whether or not the taxpayer is insolvent, and the amount by which the taxpayer
is insolvent, shall be determined on the basis of the taxpayer’s assets and
liabilities immediately before the discharge.

```catala
scope TaxReturn:
  definition taxpayer.insolvency equals
    # This is a nonnegative number
    if taxpayer.liabilities >$ taxpayer.fmv_assets then
      taxpayer.liabilities -$ taxpayer.fmv_assets
    else
      $0
    # Ideally, we would like a built-in operator: (TODO)
    # excess_of (assets, liabilities)

  definition taxpayer.is_insolvent equals
    taxpayer.insolvency >$ $0
```

108(a)(1)

In general: Gross income does not include any amount which (but for this
subsection) would be includible in gross income by reason of the discharge (in
whole or in part) of indebtedness of the taxpayer if—
(...)
(B) the discharge occurs when the taxpayer is insolvent,

```catala
scope DischargeOfIndebtedness:
  label delta_2
  exception delta_3
  definition gross_income11
  under condition
    taxpayer.is_insolvent
  consequence equals
    $0
```

108(a)(3)

Insolvency exclusion limited to amount of insolvency: In the case of a discharge
to which paragraph (1)(B) applies, the amount excluded under paragraph (1)(B)
shall not exceed the amount by which the taxpayer is insolvent.

```catala
scope DischargeOfIndebtedness:
  label delta_1
  exception delta_2
  definition gross_income11
  under condition
    taxpayer.is_insolvent
  consequence equals
    (if taxpayer.insolvency >$ taxpayer.discharge_of_indebtedness then
      taxpayer.discharge_of_indebtedness
    else
      taxpayer.insolvency)
    # Ideally, we would like a built-in operator to write:
    # min of (taxpayer.insolvency, taxpayer.cancellation_of_indebtedness)
```
